plugins {
    id 'java'
    id 'java-gradle-plugin'
    id "io.freefair.lombok" version "6.4.1"
    id 'net.saliman.properties' version '1.5.2'

    // Publishing publicly
    id "com.gradle.plugin-publish" version "0.20.0"

    // Publishing to Artifactory
    id 'maven-publish'
}

group 'io.github.crimix'
version '1.8'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-exec:1.3'
    testImplementation(platform('org.junit:junit-bom:5.8.2'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.0')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.9.0')
    testImplementation("org.assertj:assertj-core:3.23.1")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

gradlePlugin {
    plugins {
        changedProjectsPlugin {
            id = 'io.github.crimix.changed-projects-task'
            implementationClass = 'io.github.crimix.changedprojectstask.ChangedProjectsPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/Crimix/ChangedProjectsTaskPlugin'
    vcsUrl = 'https://github.com/Crimix/ChangedProjectsTaskPlugin'

    plugins {
        changedProjectsPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Changed Projects Task Plugin'
            description = 'A Gradle plugin to run a user defined task on changed projects (modules) and their dependent projects (modules)'
            tags = ['git', 'project dependencies', 'task', 'changed projects', 'changed modules']
        }
    }
}

publishToMavenLocal.dependsOn(validatePlugins)
publishPlugins.dependsOn(validatePlugins)